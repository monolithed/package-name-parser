/*
 @see https://docs.npmjs.com/misc/scope
 @see https://docs.npmjs.com/files/package.json
*/

{
    const INVALID_LEADING_CHARACTERS = /^[._]/;
    const MAX_SIZE = 214;
}

/*
 * --------------------
 * Package
 * --------------------
*/

Main = input:Package {
    const {length} = text();

    if (length > MAX_SIZE) {
        return error(`The name must be less than or equal to ${MAX_SIZE} characters`);
    }

    return input;
};

Package = PackageWithVersion / PackageWithScope / PackageName;

/*
 * --------------------
 * Name
 * --------------------
*/

Id = id:$[a-z0-9_.-]+ {
    const position = id.search(INVALID_LEADING_CHARACTERS);

    if (position !== -1) {
        const {start} = location();

        return error(`Unexpected character "${id[position]}" at position ${start.column}.`);
    }

    return id;
};

PackageName = name:Id {
    return {name};
};

/*
 * --------------------
 * Scope
 * --------------------
*/

PackageWithScope = "@" scope:Id "/" name:Id {
    return {scope, name};
};

/*
 * --------------------
 * Version
 * --------------------
*/

Integer = integer:$[0-9]+ {
    return integer
};

MajorVersion = Integer;
MinorVersion = Integer "." Integer;
PatchVersion = Integer "." Integer "." Integer;

Version = $(PatchVersion / MinorVersion / MajorVersion);

PackageWithVersion =
    name:(PackageWithScope / PackageName) "@" version:Version {
        return {...name, version};
    }
;
